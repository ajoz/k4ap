apply plugin: 'jacoco'

/**
 * The correct path of the report is $rootProjectDir/app/build/reports/jacoco/index.html
 * to run this task use: ./gradlew clean jacocoTestReport
 *
 * Unit tests generate .exec file and Android Instrumentation generates .ec file. We do not need
 * to convert from one format to another. We just need to ensure that that the Android Instrumentation
 * coverage task is executed first (because there is almost none possibilities to configure it). Next
 * we need to run unit tests (Robolectric, plain JUnit) and then create coverage data with both
 * files (ec and exec).
 *
 * This is why our jacocoTestReport relies on "createDebugCoverageReport" task.
 *
 */
task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {
    group = "reporting"
    description = "Generate unified Jacoco code coverage report"

    reports {
        xml.enabled = false
        html.enabled = true
        csv.enabled = false
        xml.destination = "${project.buildDir}/reports/jacocoTestReport.xml"
        html.destination = "${project.buildDir}/reports/jacoco"
        csv.destination = "${project.buildDir}/reports/jacocoTestReport.csv"
    }

    def fileFilter = [
            '**/*Test*.*',
            '**/AutoValue_*.*',
            '**/*JavascriptBridge.class',
            '**/R.class',
            '**/R$*.class',
            '**/Manifest*.*',
            'android/**/*.*',
            '**/BuildConfig.*',
            '**/*$ViewBinder*.*',
            '**/*$ViewInjector*.*',
            '**/Lambda$*.class',
            '**/Lambda.class',
            '**/*Lambda.class',
            '**/*Lambda*.class',
            '**/*$InjectAdapter.class',
            '**/*$ModuleAdapter.class',
            '**/*$ViewInjector*.class',
            '**/*_MembersInjector.class', //Dagger2 generated code
            '*/*_MembersInjector*.*', //Dagger2 generated code
            '**/*_*Factory*.*', //Dagger2 generated code
            '*/*Component*.*', //Dagger2 generated code
            '**/*Module*.*' //Dagger2 generated code
    ]
    def coverageSourceDirs = [
                  "${project.projectDir}/src/main/java",
                  "${project.projectDir}/src/main/kotlin"
                  ]
    
    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    classDirectories = fileTree(dir: "${project.buildDir}/intermediates/classes/debug", excludes: fileFilter)
    executionData = fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec", //we use "debug" build type for test coverage (can be other)
            "outputs/code-coverage/connected/*coverage.ec"
            // Android Gradle plugin 2.2.+ generates a coverage file for each execution
            // generated files have device / emulator in the name, so we need to pass every file
            // we know that the file ends with *coverage.ec
    ])
}
